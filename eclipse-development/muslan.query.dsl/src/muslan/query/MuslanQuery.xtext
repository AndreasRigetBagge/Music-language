grammar muslan.query.MuslanQuery with org.eclipse.xtext.common.Terminals

import "https://www.example.org/query"
import "https://www.example.org/musiccollection" as musiccollection
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Query returns Query:
	{Query}
	"pick" type=CategorizedElementType "from" musiccollection=[musiccollection::MusicCollection|EString] "with"
	filter=FilterClause
;


enum CategorizedElementType:
	Track = 'track' | Album = 'album'
;


EString returns ecore::EString:
	STRING | ID
;


FilterClause:
	 Clause | OrClause | AndClause
;


Clause:
	CategoryClause | ArtistClause | TitleClause
;


CategoryClause:
	category=CategoryType operator=OperatorType value=STRING
;


enum CategoryType returns musiccollection::CategoryType:
				Rating = 'rating' | Genre = 'genre' | Tag = 'tag'
;


ArtistClause:
	"artist" operator=RestrictedOperatorType value=STRING
;


TitleClause:
	"title" operator=RestrictedOperatorType value=STRING
;


enum OperatorType returns OperatorType:
				SameAs = '=' | DifferentFrom = 'not' | LessThan = '<' | GreaterThan = '>';


enum RestrictedOperatorType returns OperatorType:
				SameAs = '=' | DifferentFrom = 'not';


OrClause:
	"either" "(" filterclause+=OrContent (("or"|",")? filterclause+=OrContent)+ ")"
;


OrContent returns FilterClause:
	Clause | AndClause
;


AndClause:
	"both" "(" filterclause+=AndContent (("and"|",")? filterclause+=AndContent)+ ")"
;

AndContent returns FilterClause:
	Clause | OrClause
;