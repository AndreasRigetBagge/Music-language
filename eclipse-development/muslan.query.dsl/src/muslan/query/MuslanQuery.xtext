grammar muslan.query.MuslanQuery with org.eclipse.xtext.common.Terminals

import "https://www.example.org/query"
import "https://www.example.org/collection" as collection
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Query returns Query:
	{Query}
	"pick" type=PlayItemType "from" collection=[collection::Collection|EString] "with"
	filter=FilterClause
;


enum PlayItemType:
	Track = 'track' | Album = 'album'
;


EString returns ecore::EString:
	STRING | ID
;


FilterClause:
	 Clause | OrClause | AndClause
;


Clause:
	CategoryClause | ArtistClause | TitleClause
;


CategoryClause:
	category=CategoryType operator=OperatorType value=STRING
;


enum CategoryType returns collection::CategoryType:
				Rating = 'rating' | Genre = 'genre' | Tag = 'tag'
;


ArtistClause:
	"artist" operator=RestrictedOperatorType value=STRING
;


TitleClause:
	"title" operator=RestrictedOperatorType value=STRING
;


enum OperatorType returns OperatorType:
				SameAs = '=' | DifferentFrom = 'not' | LessThan = '<' | GreaterThan = '>';


enum RestrictedOperatorType returns OperatorType:
				SameAs = '=' | DifferentFrom = 'not';


OrClause:
	"either" "(" OrContent ")"
;


OrContent returns OrClause:
	filterclause+=FilterClause "or" filterclause+=FilterClause
;


AndClause:
	"both" "[" AndContent "]"
;

AndContent returns AndClause:
	filterclause+=FilterClause "and" filterclause+=FilterClause
;